apply plugin: 'groovy'
apply plugin: 'java'

javadoc.options.encoding = 'UTF-8'

group = GROUP
version = VERSION_NAME

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', "${buildDir}/gen/main/java"]
        }

        groovy {
            srcDir 'src/main/groovy'
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'com.android.tools.build:gradle:2.2.2'
    compile 'com.google.code.gson:gson:2.5'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile 'org.gmock:gmock:0.8.2'
}

import org.apache.tools.ant.filters.ReplaceTokens

task generateVersionClass(type: Copy) {
    from 'src/main/templates/Version.java'
    into "${buildDir}/gen/main/java/com/zen/android/puck"
    filter(ReplaceTokens, tokens: [version: version])
    outputs.upToDateWhen { false }
}

compileJava.dependsOn generateVersionClass

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from 'build/docs/groovydoc'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

artifacts {
    archives jar
    archives groovydocJar
    archives javadocJar
}

apply from: "${rootDir}/gradle/gradle-mvn-push.gradle"
